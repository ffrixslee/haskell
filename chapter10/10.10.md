# Chapter exercises
## Warm-up and review
For the following set of exercises, you are not expected to use folds. These are intended to review material from previous chapters. Feel free to use any syntax or structure from previous chapters that seems appropriate.  

1. Given the following sets of consonants and vowels:  
stops = "pbtdkg"  
vowels = "aeiou"  
a) Write a function that takes inputs from stops and vowels and makes 3-tuples of all possible stop-vowel-stop combinations.  
These will not all correspond to real words in English, although the stop-vowel-stop pattern is common enough that many of them will.  

stopVowelstop 

```hs
stops = "pbtdkg"
vowels = "aeiou"
 
triples :: [a] -> [a] -> [(a, a, a)]
triples xs ys = [(x, y, z) | x <- xs, y <- ys, z <- xs]

stopVowelStop = triples stops vowels
```

b) Modify that function so that it only returns the combinations that begin with a p. 

```hs
startWithP = [(a, b, c) | (a, b, c) <- triples stops vowels, a == 'p']
```

c) Now set up lists of nouns and verbs (instead of stops
and vowels), and modify the function to make tuples
representing possible noun-verb-noun sentences.

```hs
nounVerbNoun = triples nouns verbs
```

2. What does the following mystery function do? What is
its type? Try to get a good sense of what it does before
you test it in the REPL to verify it:  
seekritFunc x =  
div (sum (map length (words x)))  
(length (words x))  
> The function counts the number of letters in the word.
> seekritFunc :: String -> Int

3. We’d really like the answer to be more precise. Can you
rewrite that using fractional division?

```hs
seekritFunc' x =
   fromIntegral (sum (map length (words x))) /
   fromIntegral (length (words x))
```

## Rewriting functions using folds
In the previous chapter, you wrote these functions using direct recursion over lists. The goal now is to rewrite them using folds.  
Where possible, to gain a deeper understanding of folding, try rewriting the fold version so that it is point-free.  
Point-free versions of these functions written with a fold
should look like this:  
myFunc = foldr f z  
So, for example, with the and function:  
-- direct recursion, not using &&  
myAnd :: [Bool] -> Bool  
myAnd [] = True  
myAnd (x:xs) =  
if x == False  
then False  
else myAnd xs  
-- direct recursion, using &&  
myAnd :: [Bool] -> Bool  
myAnd [] = True  
myAnd (x:xs) = x && myAnd xs  

-- fold, not point-free  
myAnd :: [Bool] -> Bool  
myAnd = foldr  
(\a b ->  
if a == False  
then False  
else b) True  
-- fold, both myAnd and the folding  
-- function are point-free now    
myAnd :: [Bool] -> Bool  
myAnd = foldr (&&) True  

The goal here is to converge on the final version where
possible. You don’t need to write all variations for each example, but the more variations you write, the deeper your
understanding of these functions will become.  
1. myOr returns True if any Bool in the list is True:  
myOr :: [Bool] -> Bool  
myOr = undefined  

```hs
myOr' :: [Bool] -> Bool
myOr' = foldr (||) False
```

2. myAny returns True if a -> Bool applied to any of the values in the list returns True:  
myAny :: (a -> Bool) -> [a] -> Bool  
myAny = undefined  

Example for validating myAny:  
Prelude> myAny even [1, 3, 5]  
False  
Prelude> myAny odd [1, 3, 5]  
True  

```hs
-- recursive
myAny :: (a -> Bool) -> [a] -> Bool
myAny f = foldr (\x res -> res || f x) False
-- fold / point-free
myAny' :: (a -> Bool) -> [a] -> Bool
myAny' f = myOr . map f
```

3. Write two versions of myElem. One version should use
folding and the other should use any:  
myElem :: Eq a => a -> [a] -> Bool  
Prelude> myElem 1 [1..10]  
True  
Prelude> myElem 1 [2..10]  
False  

```hs
-- recursive
myElem :: Eq a => a -> [a] -> Bool
myElem _ []     = False
myElem x (y:ys) = x == y || myElem x ys
-- fold / point-free
myElem' :: Eq a => a -> [a] -> Bool
myElem' x = foldr (\y res -> x == y || res) False
-- fold / point-free
myElem'' :: Eq a => a -> [a] -> Bool
myElem'' x = foldr ((||) . (x ==)) False
```

4. Implement myReverse. Don’t worry about trying to make
it lazy:  
myReverse :: [a] -> [a]  
myReverse = undefined  
Prelude> myReverse "blah"  
"halb"  
Prelude> myReverse [1..5]  
[5,4,3,2,1]  

```hs
-- recursive
myReverse :: [a] -> [a]
myReverse xs = go xs []
    where go [] ys     = ys
          go (x:xs) ys = go xs (x:ys)
-- fold / point-free
myReverse' :: [a] -> [a]
myReverse' = foldl (flip (:)) []
```

5. Write myMap in terms of foldr. It should have the same
behavior as the built-in map:  
myMap :: (a -> b) -> [a] -> [b]  
myMap = undefined  

```hs
-- recursive
myMap :: (a -> b) -> [a] -> [b]
myMap f []     = []
myMap f (x:xs) = f x : myMap f xs
-- fold / point-free
myMap' :: (a -> b) -> [a] -> [b]
myMap' f = foldr ((:) . f) []
```

6. Write myFilter in terms of foldr. It should have the same
behavior as the built-in filter:  
myFilter :: (a -> Bool) -> [a] -> [a]  
myFilter = undefined  

```hs
-- recursive
myFilter :: (a -> Bool) -> [a] -> [a]
myFilter f [] = []
myFilter f (x:xs) = if f x
                    then x : myFilter f xs
                    else myFilter f xs
-- fold / point-free
myFilter' :: (a -> Bool) -> [a] -> [a]
myFilter' f = foldr consIf  []
    where  x `consIf` res | f x = x : res
                          | otherwise = res
```

7. squish flattens a list of lists into a list:  
squish :: [[a]] -> [a]  
squish = undefined  

```hs
-- recursive
squish :: [[a]] -> [a]
squish []       = []
squish (xs:xss) = xs ++ squish xss
-- fold / point-free
squish' :: [[a]] -> [a]
squish' = foldr (++) []
```

8. squishMap maps a function over a list and concatenates the result:  
squishMap :: (a -> [b]) -> [a] -> [b]  
squishMap = undefined  
Prelude> squishMap (\x -> [1, x, 3]) [2]  
[1,2,3]  
Prelude> f x = "WO " ++ [x] ++ " OT "  
Prelude> squishMap f "blah"  
"WO b OT WO l OT WO a OT WO h OT "  

```hs
-- recursive
squishMap :: (a -> [b]) -> [a] -> [b]
squishMap f []       = []
squishMap f (xs:xss) = f xs ++ squishMap f xss
 
-- fold / point-free
squishMap' :: (a -> [b]) -> [a] -> [b]
squishMap' f = foldr ((++) . f) []
 
squishMap'' :: (a -> [b]) -> [a] -> [b]
squishMap'' f = squish . map f
```


9. squishAgain flattens a list of lists into a list. This time, reuse the squishMap function:  
squishAgain :: [[a]] -> [a]  
squishAgain = undefined  

```hs
squishAgain :: [[a]] -> [a]
squishAgain = squishMap id
```

10. myMaximumBy takes a comparison function and a list and
returns the greatest element of the list based on the last
value that the comparison returns GT for:  
myMaximumBy :: (a -> a -> Ordering)  
-> [a]  
-> a  
myMaximumBy = undefined  
Prelude> myMaximumBy (\_ _ -> GT) [1..10]  
1  
Prelude> myMaximumBy (\_ _ -> LT) [1..10]  
10  
Prelude> myMaximumBy compare [1..10]  
10  

```hs
-- recursive (from Chapter 9)
myMaximumBy :: (a -> a -> Ordering) -> [a] -> a
myMaximumBy _ [] = error "myMaximumBy - empty list"
myMaximumBy _ [x] = x
myMaximumBy f (x1:x2:xs) =
    case f x1 x2 of
      GT -> myMaximumBy f (x1:xs)
      _  -> myMaximumBy f (x2:xs)
 
-- fold / point-free
myMaximumBy' :: (a -> a -> Ordering) -> [a] -> a
myMaximumBy' f xs = foldr1 comparing xs
    where comparing x y = case f x y of
                            GT -> x
                            _  -> y
```

11. myMinimumBy takes a comparison function and a list and
returns the least element of the list based on the last value that the comparison returns LT for:  

myMinimumBy :: (a -> a -> Ordering)  
-> [a]  
-> a  
myMinimumBy = undefined  
Prelude> myMinimumBy (\_ _ -> GT) [1..10]  
10  
Prelude> myMinimumBy (\_ _ -> LT) [1..10]  
1  
Prelude> myMinimumBy compare [1..10]  
1  

```hs
myMinimumBy :: (a -> a -> Ordering) -> [a] -> a
myMinimumBy f xs = foldr1 comparing xs
    where comparing x y = case f x y of
                            LT -> x
                            _  -> y
```
